archivesBaseName = "kafka-group-coordinator"

configurations {
    generator
}

dependencies {
    implementation project(':server-common')
    implementation project(':clients')
    implementation project(':metadata')
    implementation libs.slf4jApi

    testImplementation project(':clients').sourceSets.test.output
    testImplementation project(':server-common').sourceSets.test.output
    testImplementation libs.junitJupiter
    testImplementation libs.mockitoCore

    testRuntimeOnly libs.slf4jlog4j

    generator project(':generator')
}

sourceSets {
    main {
        java {
            srcDirs = ["src/generated/java", "src/main/java"]
        }
    }
    test {
        java {
            srcDirs = ["src/generated/java", "src/test/java"]
        }
    }
}

javadoc {
    enabled = false
}

task processMessages(type:JavaExec) {
    mainClass = "org.apache.kafka.message.MessageGenerator"
    classpath = configurations.generator
    args = [ "-p", "org.apache.kafka.coordinator.group.generated",
             "-o", "src/generated/java/org/apache/kafka/coordinator/group/generated",
             "-i", "src/main/resources/common/message",
             "-m", "MessageDataGenerator"
    ]
    inputs.dir("src/main/resources/common/message")
            .withPropertyName("messages")
            .withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.cacheIf { true }
    outputs.dir("src/generated/java/org/apache/kafka/coordinator/group/generated")
}

compileJava.dependsOn 'processMessages'
srcJar.dependsOn 'processMessages'
